package RecipeSystemHandlePickup

import RecipeSystemGlobals
import RecipeSystem
import RecipeSystemConfig
import RecipeSystemUtil
import HashList

constant HashList<unit> activeUnits = new HashList<unit>

public function handleItemPickup()
    let itm = GetManipulatedItem()
    let itemId = itm.getTypeId()
    let user = GetManipulatingUnit()
    let itemInfo = ITEM_INFO_MAP.get(itemId)
    
    // make sure this item is in the system.
    if not itemInfo == null and not activeUnits.has(user)
        // add to the active units so that this doesn't keep looping forever
        activeUnits.add(user)
        let stackLimit = itemInfo.getStackLimit(user)
        if stackLimit <= 0
            // Drop this item, can't carry
            user.dropItemPoint(itm, user.getPos())
            // TODO warn user they can't carry the item
        else
            // we can carry this item, so stack it and craft it.
            if doItemStacking and itemInfo.isStacking()
                user.stackItem(itm)
                
            // Find all recipes containing this item, check for combos that are automatic.
            user.recipeCraftAutomatic()
        // remove from the active users
        activeUnits.remove(user)
